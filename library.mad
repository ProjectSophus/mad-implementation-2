
Module Algebra.Structures {

    Concept {Magmas, Monoids, Groups, "Abelian Groups", Rings, "Commutative Rings"}

    Template Magma [M, Op] {
        Example Magmas <M>
        
        Operation on <M> <Op>
    }

    Template Monoid [M, Op] Extends Magma [<M>, <Op>] {
        Example Monoids <M>
        
        Mop = $"$M.$Op"
        
        Example <M> $"$Mop.Identity Element"
        
        AssoText = $"$Op(a, $Op(b, c)) = $Op($Op(a, b), c)"
        IdenText = $"$Op(Identity, a) = $Op(a, Identity) = a"
        
        Javascript """
            
            if(mad.vars.Op === "Addition") {
                mad.vars.AssoText = "a + (b + c) = (a + b) + c"
                mad.vars.IdenText = "0 + a = a + 0 = a"
            }
            
            if(mad.vars.Op === "Multiplication") {
                mad.vars.AssoText = "a * (b * c) = (a * b) * c"
                mad.vars.IdenText = "1 * a = a * 1 = a"
            }
            
        """
        
        Asso = $"$Mop.Assosiativity"
        Statement <Asso> <AssoText>
        Relevant <Asso> {<M>, <Mop>}
        
        Iden = $"$Mop.Identity Rule"
        Statement <Iden> <IdenText>
        Relevant <Iden> {<M>, <Mop>}
    }

    Template Group [M, Op] Extends Monoid [<M>, <Op>] {
        Example Groups <M>
        
        Function on <M> $"Inverse of $Op"
        InvOp = $"$M.Inverse of $Op"
        
        InvText = $"$Op(a, Inverse(a)) = $Op(Inverse(a), a) = Identity"
        
        Javascript """
        
            if(mad.vars.Op === "Addition"){
                mad.vars.InvText = "a + (-a) = -a + a = 0"
            }
            
            if(mad.vars.Op === "Multiplication"){
                mad.vars.InvText = "a * a^(-1) = a^(-1) * a = 1"
            }
        
        """
        
        Inv = $"$Mop.Inverse Rule"
        Statement <Inv> <InvText>
        Relevant <Inv> {<M>, <Mop>, <InvOp>}
    }
    
    Template AbelianGroup [M, Op] Extends Group [<M>, <Op>] {
        Example "Abelian Groups" <M>
        
        AbText = $"$Op(a, b) = $Op(b, a)"
        
        Javascript """
        
            if(mad.vars.Op === "Addition"){
                mad.vars.AbText = "a + b = b + a"
            }
            
            if(mad.vars.Op === "Multiplication"){
                mad.vars.AbText = "a * b = b * a"
            }
        
        """
        
        Ab = $"$Mop.Commutativity"
        Statement <Ab> <AbText>
        Relevant <Ab> {<M>, <Mop>}
    }
    
    
    Template Ring [M] Extends {AbelianGroup [<M>, "Addition"], Monoid [<M>, "Multiplication"]} {
        Example Rings <M>
        
        //Composition $"$M.Subtraction" (\x, \y) = $"$M.Addition"(\x, $"M.Inverse of Addition"(\y))
        
        Dist = $"$M.Distributivity"
        Statement <Dist> $"a * (b + c) = a * b + a * c and (a + b) * c = a * c + b * c"
        Relevant <Dist> {<M>, $"$M.Addition", $"$M.Multiplication"}
    }
    
    Template CommutativeRing [M] Extends Ring [<M>] {
        Example "Commutative Rings" <M>
        
        Com = $"$M.Multiplication.Commutativity"
        Statement <Com> $"$a * b = b * a"
        Relevant <Com> {<M>, $"$M.Multiplication"}
    }

}

Module Algebra.Numbers {
    
    Using Algebra.Structures
    
    Concept {Z, Q, Qbar, R, C}
    UseTemplate Ring {[Z], [Q], [Qbar], [R], [C]}
    
    Generalization Q Z
    Generalization Qbar Q
    Generalization R Q
    Generalization C R
    Generalization C Qbar
    
    Antiexample C {potato, cucumber}
    Example Z {-1, 0, 1}
    Example ~AntiexampleForSpecialization Q {"1/2", "0.434", "-5.5"}
    Example ~AntiexampleForSpecialization Qbar {"sqrt(2)", "golden ratio", "i", "2i + 3", "2e^(3/4 pi i)"}
    Example ~AntiexampleForSpecialization R {pi, e}
    Example ~AntiexampleForSpecialization C {"i*pi + 3", "e^i"}
    
    Representation of {Z, Q, R} {Decimal, Binary, Hexadecimal}
    Representation of {Qbar, C} {Cartesian, Polar}
    Representation of {Z, Q} "Factorized Form"
    Representation of {Qbar, R, C} "As zero of polynomial in an interval"
    
    Relation on {Z, Q, R} {"Less-than", "Greater-than", "Less-than or equal", "Greater than or equal"}
    
    Description Z """
Z is the ring of the whole numbers.
It contains both positive and negative numbers.
The distance between two numbers is given by the absolute value of the difference."""
    
}
